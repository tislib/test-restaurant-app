jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'PACKAGE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.85
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.85
            }
            excludes = [
                    'net.tislib.restaurantapp',
                    'net.tislib.restaurantapp.data',
                    'net.tislib.restaurantapp.data.authentication',
                    'net.tislib.restaurantapp.data.mapper',
                    'net.tislib.restaurantapp.exception',
                    'net.tislib.restaurantapp.model',
                    'net.tislib.restaurantapp.const'
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

checkstyle {
    toolVersion = '8.33'
    configFile = 'gradle/checkstyle.xml' as File
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

pmd {
    consoleOutput = true
    toolVersion = "6.21.0"
    rulePriority = 5
    ruleSets = ["gradle/pmd.xml"]
}
