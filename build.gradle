plugins {
    id 'org.springframework.boot' version '2.3.9.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'io.freefair.lombok' version '5.1.0'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
}

apply from: 'gradle/checks.gradle';

group = 'net.tislib.restaurantapp'
version = '1.0'
sourceCompatibility = '1.11'

repositories {
    mavenCentral()
}

dependencies {
    // spring boot and its dependencies
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

    // jwt token helper library

    implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
    runtime 'io.jsonwebtoken:jjwt-impl:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // database
    runtimeOnly 'org.postgresql:postgresql'

    //spring metrics
    implementation group: 'io.micrometer', name: 'micrometer-registry-prometheus', version: micrometerVersion

    // mapstruct
    implementation group: 'org.mapstruct', name: 'mapstruct-jdk8', version: mapstructVersion
    annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: mapstructVersion


    // rest api documentation
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: springDocVersion

    // database versioning
    implementation group: 'org.flywaydb', name: 'flyway-core'

    // h2 embedded database for integration tests and uat
    implementation group: 'com.h2database', name: 'h2'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

test {
    useJUnitPlatform()
//    finalizedBy jacocoTestReport
}
